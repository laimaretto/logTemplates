#Command: /show port detail
#Timos: 21
#Version: 1.0.0
Value Required Interface (\S+)
Value Description (.+)
Value OperSpeed (.+)
Value ConfigSpeed (.+)
Value AdminState ([uU][pP]|[Dd][oO][wW][nN])
Value OperDuplex (.+)
Value OperState (.+)
Value ConfigDuplex (.+)
Value PhysicalLink (\S+)
Value MTU (\S+)
Value SingleFiberMode (\S+)
Value ConfiguredMode (\S+)
Value EncapType (\S+)
Value NetEgrQueuePol (\S+)
Value EgrSchedPol (\S+)
Value AutoNegotiate (\S+)
Value MDI_MDX (\S+)
Value DownWhenLooped (\S+)
Value LoopDetected (\S+)
Value Retry (\S+)
Value SSMCodeType (\S+)
Value LinkLengthsupport (.+)
Value TxOutPwr_dBm_VALUE (\S+)
Value TxOutPwr_dBm_HighAlarm (\S+)
Value TxOutPwr_dBm_HighWarn (\S+)
Value TxOutPwr_dBm_LowWarn (\S+)
Value TxOutPwr_dBm_LowAlarm (\S+)
Value RxInPwr_dBm_VALUE (\S+)
Value RxInPwr_dBm_HighAlarm (\S+)
Value RxInPwr_dBm_HighWarn (\S+)
Value RxInPwr_dBm_LowWarn (\S+)
Value RxInPwr_dBm_LowAlarm (\S+)

Start
  ^\w:\S+(#|>) /show port detail
  ^.\w:\S+(#|>) /?show port detail
  ^Description\s+:\s${Description}
  ^Interface\s+:\s${Interface}\s+Oper\sSpeed\s+:\s${OperSpeed}
  ^Link.level\s+:\s\S+\s+Config\sSpeed\s+:\s${ConfigSpeed}
  ^Admin\sState\s+:\s${AdminState}\s+Oper\sDuplex\s+:\s${OperDuplex}
  ^Oper\sState\s+:\s${OperState}\s+Config\sDuplex\s+:\s${ConfigDuplex}
  ^Physical\sLink\s+:\s${PhysicalLink}\s+MTU\s+:\s${MTU}
  ^Single\sFiber\sMode\s+:\s${SingleFiberMode}
  ^Configured\sMode\s+:\s${ConfiguredMode}\s+Encap\sType\s+:\s${EncapType}
  ^Net. Egr. Queue Pol\s?.\s${NetEgrQueuePol}
  ^Egr. Sched. Pol\s+.\s${EgrSchedPol}
  ^Auto-negotiate\s+:\s${AutoNegotiate}\s+MDI/MDX\s+:\s${MDI_MDX}
  ^Down.when.looped\s+.\s${DownWhenLooped}.+
  ^Loop Detected\s+.\s${LoopDetected}\s+Retry\s+.\s${Retry}
  ^SSM Code Type\s+.\s${SSMCodeType}
  ^Link\sLength\ssupport:\s${LinkLengthsupport}
  ^Tx\sOutput\sPower\s.dBm.\s+${TxOutPwr_dBm_VALUE}\s+${TxOutPwr_dBm_HighAlarm}\s+${TxOutPwr_dBm_HighWarn}\s+${TxOutPwr_dBm_LowWarn}\s+${TxOutPwr_dBm_LowAlarm}
  ^Rx\sOptical\sPower\s.avg dBm.\s+${RxInPwr_dBm_VALUE}\s+${RxInPwr_dBm_HighAlarm}\s+${RxInPwr_dBm_HighWarn}\s+${RxInPwr_dBm_LowWarn}\s+${RxInPwr_dBm_LowAlarm}
  ^Ethernet Bandwidth Notification Message Information -> Record
